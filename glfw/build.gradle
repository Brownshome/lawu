/*
 * Copyright James Brown 2023
 * Author: James Brown
 */

plugins {
	id 'dev.brownjames.lawu.java-library'
	id 'io.github.krakowski.jextract' version '0.4.2'
	id 'maven-publish'
}

group 'dev.brownjames.lawu'
version '0.1.0-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
	options.fork = true
	options.forkOptions.memoryMaximumSize = '2g'
}

import org.apache.tools.ant.taskdefs.condition.Os

jextract {
	header("${project.projectDir}/glfw/include/GLFW/glfw3.h") {
		targetPackage = 'dev.brownjames.lawu.glfw.bindings'

		if (Os.isFamily(Os.FAMILY_WINDOWS)) {
			libraries = [ 'glfw3', 'vulkan-1' ]

			definedMacros = [ 'VK_USE_PLATFORM_WIN32_KHR', '_WIN32', 'GLFW_INCLUDE_VULKAN', 'GLFW_INCLUDE_NONE', 'GLFW_DLL' ]
			includes = [
					"${System.env.VULKAN_SDK}/Include",
					'C:\\Program Files\\Microsoft Visual Studio\\2022\\Community\\VC\\Tools\\MSVC\\14.36.32532\\include'
			]
		} else {
			libraries = [ 'glfw', 'vulkan' ]

			definedMacros = [ 'GLFW_INCLUDE_VULKAN', 'GLFW_INCLUDE_NONE' ]
			includes = [
					"${System.env.VULKAN_SDK}/include"
			]
		}
	}
}

publishing {
	repositories {
		maven {
			name = 'GithubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/lawu')
			credentials {
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId 'glfw'
			from components.java

			pom {
				name = 'lawu'
				description = 'Glfw bindings for Java using JExtract and the foreign function API'
				url = 'https://maven.pkg.github.com/Brownshome/lawu'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'james.antony.brown@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/lawu.git'
					url = 'https://github.com/Brownshome/lawu'
				}
			}
		}
	}
}
