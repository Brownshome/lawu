/*
 * Copyright James Brown 2023
 * Author: James Brown
 */

plugins {
	id 'java-library'
	id 'io.github.krakowski.jextract' version '0.4.2'
	id 'com.github.mrsarm.jshell.plugin' version '1.2.1'
	id 'maven-publish'
}

group 'dev.brownjames.lawu'
version '0.1.0-SNAPSHOT'

repositories {
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
}

test {
	useJUnitPlatform()
}

dependencies {
	api('de.skuzzle:semantic-version') {
		version {
			strictly '[2.1.0, 3)'
		}
	}

	testImplementation('org.junit.jupiter:junit-jupiter') {
		version {
			strictly '[5.7.1, 6)'
		}
	}
}

javadoc {
	options {
		source = java.sourceCompatibility.majorVersion
		encoding 'UTF-8'
		addBooleanOption('-enable-preview', true)
		addStringOption('-module-source-path', "dev.brownjames.lawu.vulkan=src/main/java;build/generated/sources/jextract/main/java")
	}

	exclude 'dev/brownjames/lawu/vulkan/bindings/**'
}

tasks.withType(JavaCompile).configureEach {
	options.javaModuleVersion = provider { project.version.toString() }
	options.encoding = 'UTF-8'
	options.compilerArgs += '--enable-preview'
	options.fork = true
	options.forkOptions.memoryMaximumSize = '2g'
}

test {
	useJUnitPlatform()
	enableAssertions true
}

import org.apache.tools.ant.taskdefs.condition.Os

jextract {
	header("${System.env.VULKAN_SDK}/include/vulkan/vulkan.h") {
		libraries = [ 'vulkan-1' ]
		targetPackage = 'dev.brownjames.lawu.vulkan.bindings'
		if (Os.isFamily(Os.FAMILY_WINDOWS)) {
			libraries = [ 'vulkan-1' ]

			definedMacros = [ 'VK_USE_PLATFORM_WIN32_KHR', '_WIN32' ]
			includes = [
					"${System.env.VULKAN_SDK}/Include",
					'C:\\Program Files\\Microsoft Visual Studio\\2022\\Community\\VC\\Tools\\MSVC\\14.36.32532\\include'
			]
		} else {
			libraries = [ 'vulkan' ]
			includes = [
					"${System.env.VULKAN_SDK}/include"
			]
		}
	}
}

jshell {
	args '--enable-preview'
}

publishing {
	repositories {
		maven {
			name = 'GithubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/lawu')
			credentials {
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId 'vulkan'
			from components.java

			pom {
				name = 'lawu'
				description = 'Vulkan bindings for Java using JExtract and the foreign function API'
				url = 'https://maven.pkg.github.com/Brownshome/lawu'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'james.antony.brown@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/lawu.git'
					url = 'https://github.com/Brownshome/lawu'
				}
			}
		}
	}
}
